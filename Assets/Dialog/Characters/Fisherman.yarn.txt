title: Fisherman
tags: 
colorID: 0
position: 721,130
---
<<if $key is 0>>
	Gi: Hello there young lad!
	<<if $fish is 0>>
		<<if ! $fishermanSpoke and ! $attempted>>
			Gi: Ya look like ya come from aways!
			Y: That is correct.	
			Gi: Well, what can a fisherman like me do for yer?
			Y: The guy at the fruit stand said I could get a fish from you to feed the homeless guy?
			Gi: Oh, Kyran? Bless his heart. Always fishin' for the old homeless guy.
			Y: So he doesn't just get a fish from you?
			Gi: Do I look like a fish bank?
			Y: ...Yes?
			Gilroy shakes his head.
			Gi: Tell me, son. Have ya ever fished before?
			[[Yes.|Fisherman.NoTutorial]]
			[[No. Can you tell me how?|Fisherman.Tutorial]]
			[[I dont want to fish yet.|Fisherman.Exit]]
		<<elseif $attempted>>
			Gi: So that fish got away from yer. Would you like to try again?
			[[Yes.|Fisherman.NoTutorial]]
			[[How do I Fish again?|Fisherman.Tutorial]]
			[[No.|Fisherman.Exit]]
		<<else>>
			Gi: Would yer like to fish now?
			[[Yes.|Fisherman.NoTutorial]]
			[[How do I Fish again|Fisherman.Tutorial]]
			[[No.|Fisherman.Exit]]
		<<endif>>
	<<endif>>
	<<if $fish is 1>>
		Gi: Did ya give him tha fish, sonny?
	<<endif>>
<<endif>>
<<if $q is 2>>
	Gi: What is life without fishin'?
<<endif>>
<<if $q is 3>>
	Gi: I just missed the biggest catch of this season!
<<endif>>
<<if $q is 4>>
	Gi: I remember my greatest catch like it was yesterday...
<<endif>>
<<if $q is 5>>
	Gi: Just me and my fishin' rod against the great ocean.
<<endif>>
===

title: Fisherman.NoTutorial
tags: 
colorID: 0
position: 721,130
---
<<set $attempted to true>>
<<start fishing game>>
===

title: Fisherman.Tutorial
tags: 
colorID: 0
position: 721,130
---
Gi: Okay, so to get the fish, ya have to lower yer hook into da water by pressing the "Down Arrow Key".
Gi: Be careful for debris! They will knock yer worm right off!
Gi: Reel yer fish back in when yer've caught it by pressing "Up Arrow Key".
Gi: I've only got 5 worms to give ya right now, so use 'em wisely.
[[Start Fishing.|Fisherman.NoTutorial]]
[[Nevermind.|Fisherman.Exit]]
===

title: Fisherman.Exit
tags: 
colorID: 0
position: 721,130
---
<<set $fishermanSpoke to true>>
Gi: Bye.
===